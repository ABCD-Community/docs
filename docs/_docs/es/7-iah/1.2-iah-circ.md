---
title: Enlace con circulación y préstamo
permalink: /docs/es/7-iah/iah-circ
lang: es
key: docs-iah-circ-es
---


Para consultar desde el iAH la disponibilidad de préstamos haga lo siguiente:

  
1. Utilizando la opción Configurar base de datos en el IAH, modificar el archivo .def de la base de datos

-   Si el sistema de préstamos trabaja con la base de datos  **[copies](https://www.mediawiki.org/w/index.php?title=Copies&action=edit&redlink=1 "Copies (page does not exist)")**  y  **[loanobjects](https://www.mediawiki.org/w/index.php?title=Loanobjects&action=edit&redlink=1 "Loanobjects (page does not exist)")**, agregar la referencia a las bases de datos de copias habilitadas para préstamo (loanobjects)

        FILE loanobjects.*=%path_database%loanobjects/data/loanobjects.*

-   Agregar la referencia a las transacciones de préstamo (**[trans](https://www.mediawiki.org/w/index.php?title=Trans&action=edit&redlink=1 "Trans (page does not exist)")**)

        FILE trans.*=%path_database%trans/data/trans.* 

Tanto las transacciones (trans) como los objetos de préstamo (loanobjects) son utilizadas en los formatos para mostrar los ejemplares disponibles

  

2. Modificar el formato de presentación del registro para incluir los comandos necesarios para la visualización de las copias habilitadas para préstamo. Este formato tendrá diferentes presentaciones según se trabaje o no con la base de datos  **[copies](https://www.mediawiki.org/w/index.php?title=Copies&action=edit&redlink=1 "Copies (page does not exist)")**

-   Si se trabaja con la base de datos copies, vea  **[Formatos de salida que integran la información de bases de datos relacionadas](https://www.mediawiki.org/w/index.php?title=Formatos_de_salida_que_integran_la_informaci%C3%B3n_de_bases_de_datos_relacionadas&action=edit&redlink=1 "Formatos de salida que integran la información de bases de datos relacionadas (page does not exist)")**.

Ejemplo de un formato que presenta las copias habilitadas para préstamo desde  [loanobjects](https://www.mediawiki.org/w/index.php?title=Loanobjects&action=edit&redlink=1 "Loanobjects (page does not exist)")  y determina si cada ejemplar está prestado, colocando su fecha de devolución:

     ref(['loanobjects']l(['loanobjects'],'CN_biblo_'v2),
       '<p><table border=0>'
       '<td width=150></td><td colspan=3><b>Total disponibilidad</b>: 'f(nocc(v959),1,0)'</td>'
        '<tr><td width=150>  </td>'
       '<td>No.inventario</td><td>Biblioteca</td><td>Tipo  de material</td>'
       '<td>Situación</td><td>Fecha devolución</td>'   
       (if p(v959) then 
           '<tr><td width=150></td><td align=center>'v959^i'</td><td align=center>'v959^l
           '</td><td align=center>'v959^o'</td>' 
           '<td>',
           if npost(['trans'],'TR_P_'v959^i) = 0 then 
               'Disponible</td><td></td><td>'
               if iocc>1 then 
                  `<a href='javascript:Reservar("`v959^i`","`v1[1]`")'>Reservar</a></td>`,
               fi
           else 
               'Prestado</td><td>' ref(['trans']l(['trans'],'TR_P_'v959^i),
                     v40[1]*6.2,'/',v40[1]*4.2,'/',v40[1].4),'</td>' 
           fi 
       fi/)
       '</table>',
    )

-   Si las existencias se desarrollan al interior del registro catalográfico, esto es, no se trabaja con la base de datos copies, el formato para presentación de la disponibilidad puede ser el siguiente:

        if p(v101) then
                '<p><table bgcolor=#cccccc>
                    <td colspan=7 align=center bgcolor=white><strong>Existencias</strong></td>
                    <tr>
                    <td bgcolor=white>Inventario</td>
                    <td bgcolor=white nowrap>Tipo obj.</td>
                    <td bgcolor=white>Vol.</td>
                    <td bgcolor=white>Ej.</td>
                    <td bgcolor=white>Ubicac.</td>
                    <td bgcolor=white>Situación</td>
                    <td bgcolor=white>Fecha devolución</td>          
                    </tr>'
                (if p(v101) then '<tr>
                <td bgcolor=white>'v101^n'</td>'
                '<td bgcolor=white>'v101^t'</td>'
                '<td bgcolor=white>'v101^m'</td>'
                '<td bgcolor=white>'v101^l'</td>'
                '<td bgcolor=white>'v101^o'</td>'
                '<td  bgcolor=white>'
                if npost(['trans'],'TR_P_'v101^n) = 0 then 
                    'Disponible</td><td  bgcolor=white></td>'
                else 
                    'Prestado</td><td  bgcolor=white>' ref(['trans']l(['trans'],'TR_P_'v101^n),
                            v40[1]*6.2,'/',v40[1]*4.2,'/',v40[1].4),'</td>' 
                fi 
                '</tr>' fi/)
                    '</table>'
            fi   

Estas especificaciones, insertadas en el formato de despliegue utilizado en el opac, produce la siguiente salida:

![File:Iah prestar 01.png](/wiki/docs/{{page.lang}}/images/Iah_prestar_01.png "File:Iah prestar 01.png")